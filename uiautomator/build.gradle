/*
 * Copyright 2014 TOYAMA Sumio
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.apache.tools.ant.*
import org.apache.tools.ant.taskdefs.condition.Os

// テストスクリプトのクラス名を定義する
def testClassName = 'com.nowsprinting.hellotesting.uiautomator.CustomerDetailTest'

// build.xmlを読み込む
ProjectHelper.configureProject(ant.project, file('build.xml'));
// local.propertiesから<AHDROID_HOME>へのパスを取得する
def sdkDir = ant.properties['sdk.dir']

// build.xmlに定義されたbuild, install, cleanターゲットを、
// それぞれ uiautomatorBuild, uiautomatorInstall, uiautomatorCleanという名前のgraldeタスクとして登録する。
['build', 'install', 'clean'].each { antTargetName ->
    task "uiautomator${antTargetName.capitalize()}" << {
        ant.project.executeTarget(antTargetName)
    }
}

task uiautomatorTest(dependsOn: ['uiautomatorBuild', 'uiautomatorInstall'], type: Exec) {
    def adbDir = "${sdkDir}/platform-tools"
    def adb = Os.isFamily(Os.FAMILY_WINDOWS) ? 'adb.exe' : 'adb'
    def adbDeviceArg = ant.properties['adb.device.arg']
    executable(file("${adbDir}/${adb}"))
    args(*adbDeviceArg.tokenize(), 'shell', 'uiautomator', 'runtest', "${ant.project.name}.jar", '-c', testClassName)
}

// Android Studioを使わないのであれば、ここから下の記述は不要。
//
// Android Studioでコード補完を有効にするために
// Android SDKのandroid.jarとuiautomator.jarをクラスパスに追加する。
// Android Studioは、build.gradle中の
// dependencies { compile ... } 
// という記載からクラスパスを構成している。
// 
apply plugin: 'java'

def targetDir = ant.properties['target']

dependencies {
    compile files("${sdkDir}/platforms/${targetDir}/android.jar")
    compile files("${sdkDir}/platforms/${targetDir}/uiautomator.jar")
}

// Android Studioで、src/直下をテストコードと認識させる
sourceSets {
    main {
        java {
            srcDir 'src'
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}
